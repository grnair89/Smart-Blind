/**
 * @author Shikha Soni
 * @author Ganesh Rajasekharan
 */
FUNCTION_BLOCK tipper	// Block definition (there may be more than one block per file)

VAR_INPUT				// Define input variables
	ambient : REAL;
	temp : REAL;
END_VAR

VAR_OUTPUT				// Define output variable
	blind : REAL;
END_VAR

FUZZIFY ambient			// Fuzzify input variable 'ambient': {'dark', 'dim' , 'bright'}
	TERM dark := (0, 1) (25, 1) (35, 0); 
	TERM dim := (25, 0) (50, 1) (75, 0);
	TERM bright := (65, 0) (75, 1) (100, 1);
END_FUZZIFY

FUZZIFY temp			// Fuzzify input variable 'temp': { 'freezing', 'cold', 'comfort', 'warm', 'hot'}
	TERM freezing := (0, 1) (25, 0);
	TERM cold := (0,0) (25,1) (50, 0);
	TERM comfort := (25, 0) (50, 1) (75, 0);
	TERM warm := (50, 0) (75, 1) (100, 0);
	TERM hot := (75, 0) (100, 1);
END_FUZZIFY

DEFUZZIFY blind			// Defzzzify output variable 'blind' : {'open', 'half', 'close' }
	TERM open := (0,1) (25,1) (50,0);
	TERM half := (25,0) (50,1) (75,0);
	TERM close := (50,0) (75,1) (100,1);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1 : IF temp IS hot AND ambient IS dim THEN blind IS close;
	RULE 2 : IF temp IS cold AND ambient IS bright THEN blind IS half; 
	RULE 3 : IF temp IS warm AND ambient IS dim THEN blind IS half;
	RULE 4 : IF temp IS warm AND ambient IS bright THEN blind IS close;
	
END_RULEBLOCK

END_FUNCTION_BLOCK

